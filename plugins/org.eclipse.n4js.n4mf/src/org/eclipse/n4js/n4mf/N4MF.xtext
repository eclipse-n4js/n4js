/**
 * Copyright (c) 2016 NumberFour AG.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 * Contributors:
 *   NumberFour AG - Initial API and implementation
 */
grammar org.eclipse.n4js.n4mf.N4MF with org.eclipse.xtext.common.Terminals

import "http://www.eclipse.org/n4js/n4mf/N4MF"
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

ProjectDescription:
		('ProjectId'		':' projectId=N4mfIdentifier)
	&	('ProjectType' 		':' projectType=ProjectType)
	&	('ProjectVersion'	':' projectVersion=DeclaredVersion)
	&	('VendorId' 		':' declaredVendorId=N4mfIdentifier)
	&	('VendorName' 		':' vendorName=STRING)?

	&	('MainModule'		':' mainModule=STRING)?

	// only available for runtime environments
	&	(extendedRuntimeEnvironment=ExtendedRuntimeEnvironment)?

	// only in case of runtime libraries or runtime environment:
	&	(providedRuntimeLibraries=ProvidedRuntimeLibraries)?

	// not available in runtime environments:
	&	(requiredRuntimeLibraries=RequiredRuntimeLibraries)?

	// only available in N4JS components (Apps, Libs, Processor)
	&	(projectDependencies=ProjectDependencies)?

	// only available in N4JS components (Apps, Libs, Processor)
	&	('ImplementationId' ':' implementationId=N4mfIdentifier)?

	// only available in N4JS components (Apps, Libs, Processor)
	&	(implementedProjects=ImplementedProjects)?

	//only RuntimeLibrary and RuntimeEnvironemnt
	&	(initModules=InitModules)?
	&	(execModule=ExecModule)?


	&	('Output' 			':' outputPathRaw=STRING)?
	&	('Libraries' 		'{' libraryPathsRaw+=STRING (',' libraryPathsRaw+=STRING)* '}')?
	&	('Resources' 		'{' resourcePathsRaw+=STRING (',' resourcePathsRaw+=STRING)* '}')?
	&	('Sources' 			'{' sourceFragment+=SourceFragment+ '}')?

	& 	('ModuleFilters'	'{' moduleFilters+=ModuleFilter+ '}')?

	&	(testedProjects=TestedProjects)?

	&	('ModuleLoader'		':' moduleLoader=ModuleLoader)?
;

enum ProjectType :
	APPLICATION = 'application' |
	PROCESSOR = 'processor' |
	LIBRARY = 'library' |
	API = 'API' |
	RUNTIME_ENVIRONMENT = "runtimeEnvironment" |
	RUNTIME_LIBRARY = "runtimeLibrary" |
	TEST = "test" |
	VALIDATION = "validation"
;

ExecModule returns ExecModule:
	{ExecModule}
	'ExecModule' ':' execModule=BootstrapModule
;


TestedProjects returns TestedProjects:
	{TestedProjects}
	'TestedProjects' '{'
		(testedProjects+=TestedProject (',' testedProjects+=TestedProject)*)?
	'}'
;


InitModules returns InitModules:
	{InitModules}
	'InitModules' '{'
		(initModules+=BootstrapModule (',' initModules+=BootstrapModule)*)?
	'}'
;

ImplementedProjects returns ImplementedProjects:
	{ImplementedProjects}
	'ImplementedProjects' '{'
		(implementedProjects+=ProjectReference (',' implementedProjects+=ProjectReference)*)?
	'}'
;

ProjectDependencies returns ProjectDependencies:
	{ProjectDependencies}
	'ProjectDependencies' '{'
		(projectDependencies+=ProjectDependency (',' projectDependencies+=ProjectDependency)*)?
	'}'
;

ProvidedRuntimeLibraries returns ProvidedRuntimeLibraries:
	{ProvidedRuntimeLibraries}
	'ProvidedRuntimeLibraries' '{'
		(providedRuntimeLibraries+=ProvidedRuntimeLibraryDependency (',' providedRuntimeLibraries+=ProvidedRuntimeLibraryDependency)*)?
	'}'
;

RequiredRuntimeLibraries returns RequiredRuntimeLibraries:
	{RequiredRuntimeLibraries}
	'RequiredRuntimeLibraries' '{'
		(requiredRuntimeLibraries+=RequiredRuntimeLibraryDependency (',' requiredRuntimeLibraries+=RequiredRuntimeLibraryDependency)*)?
	'}'
;

ExtendedRuntimeEnvironment returns ExtendedRuntimeEnvironment:
	{ExtendedRuntimeEnvironment}
	'ExtendedRuntimeEnvironment' ':' extendedRuntimeEnvironment=ProjectReference
;

//TODO build meta data not supported https://semver.org/#spec-item-10
DeclaredVersion :
	major=INT ('.' minor=INT ('.' micro=INT)?)? ('-' qualifier=N4mfIdentifier)?
;

SourceFragment:
	sourceFragmentType=SourceFragmentType '{' pathsRaw+=STRING (',' pathsRaw+=STRING)* '}'
;

enum SourceFragmentType :
	SOURCE = 'source' | EXTERNAL = 'external' | TEST = 'test'
;

ModuleFilter:
	moduleFilterType=ModuleFilterType '{'
		moduleSpecifiers+=ModuleFilterSpecifier (',' moduleSpecifiers+=ModuleFilterSpecifier)* '}'
;

BootstrapModule:
	moduleSpecifierWithWildcard=STRING ('in' sourcePath=STRING)?
;


ModuleFilterSpecifier:
	moduleSpecifierWithWildcard=STRING ('in' sourcePath=STRING)?
;

enum ModuleFilterType:
	NO_VALIDATE = 'noValidate' | NO_MODULE_WRAPPING = 'noModuleWrap'
;

ProvidedRuntimeLibraryDependency:
	project=SimpleProjectDescription
;

RequiredRuntimeLibraryDependency:
	project=SimpleProjectDescription
;

TestedProject:
	project=SimpleProjectDescription
;

/*
 * scope is optional, default scope is compile
 */
ProjectReference :
	project=SimpleProjectDescription
;

/*
 * scope is optional, default scope is compile
 */
ProjectDependency :
	project=SimpleProjectDescription
	(versionConstraint=VersionConstraint)?
	(declaredScope=ProjectDependencyScope)?
;

/*
 * vendorN4mfIdentifier is optional, if it is not specified, vendor id of current project is used.
 */
SimpleProjectDescription :
	(declaredVendorId=N4mfIdentifier ':')? projectId=N4mfIdentifier
;

/*
 * If no version range is specified, lower version is inclusive.
 */
VersionConstraint:
	(
		(exclLowerBound?='(' | '[')  lowerVersion=DeclaredVersion
			((',' upperVersion=DeclaredVersion (exclUpperBound?=')' | ']') )? | ')')
	) | lowerVersion=DeclaredVersion
;


enum ProjectDependencyScope :
	COMPILE = 'compile' | TEST = 'test'
;

enum ModuleLoader:
	N4JS = 'n4js'
|	COMMONJS = 'commonjs'
|	NODE_BUILTIN = 'node_builtin'
;

N4mfIdentifier :
	ID |
	'ProjectId' |
	'ProjectType' |
	'ProjectVersion' |
	'VendorId' |
	'VendorName' |
	'Output' |
	'Libraries' |
	'Resources' |
	'Sources' |
	'ModuleFilters' |
	'ProjectDependencies'
	'system' |
	'API' |
	'user' |
	'application' |
	'processor'
	'source' |
	'content' |
	'test'
;

@Override
terminal ID:
	'^'?('a'..'z'|'A'..'Z'|'_') ('a'..'z'|'A'..'Z'|'_'|'-'|'.'|'0'..'9')*
;
