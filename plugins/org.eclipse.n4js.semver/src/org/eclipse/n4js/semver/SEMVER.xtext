/**
 * Copyright (c) 2016 NumberFour AG.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 * Contributors:
 *   NumberFour AG - Initial API and implementation
 */

grammar org.eclipse.n4js.semver.SEMVER
	with org.eclipse.n4js.common.unicode.Unicode
	hidden (EOL)

import "http://www.eclipse.org/n4js/SEMVER"
import "http://www.eclipse.org/emf/2002/Ecore" as ecore



// This grammar is an adapted version of the BNF found at:
//  https://docs.npmjs.com/misc/semver



VersionRangeSet:
	{VersionRangeSet} WS* (ranges+=VersionRange (WS* '||' WS* ranges+=VersionRange)* WS* )?
;

VersionRange:
	VersionRangeContraint | HyphenVersionRange
;

HyphenVersionRange returns VersionRange:
	{HyphenVersionRange} from=VersionNumber WS+ '-' WS+  to=VersionNumber
;

VersionRangeContraint returns VersionRange:
	{VersionRangeConstraint} versionConstraints+=SimpleVersion (WS+ versionConstraints+=SimpleVersion)*
;

SimpleVersion:
	{SimpleVersion} (comparators+=VersionComparator WS*)* number=VersionNumber
;

VersionNumber:
	major=VersionPart
	('.' minor=VersionPart
		('.' patch=VersionPart
			('.' extended+=VersionPart)*
		)?
	)?
	(qualifier=Qualifier)?
;

VersionPart:
	wildcard?=WILDCARD | number=DIGITS
;

Qualifier:
		('-' preRelease=QualifierTag)
	|	('+' buildMetadata=QualifierTag)
	|	('-' preRelease=QualifierTag '+' buildMetadata=QualifierTag)
;

QualifierTag:
	parts+=ALPHA_NUMERIC_CHARS
	('.' parts+=ALPHA_NUMERIC_CHARS)*
;


ALPHA_NUMERIC_CHARS:
	ALPHA_NUMERIC_CHAR+
;

WILDCARD:
	'x' | 'X' | '*'
;



fragment ALPHA_NUMERIC_CHAR:
	"-"? (DIGITS | LETTERS)
;

terminal LETTERS:
	LETTER+
;

terminal DIGITS returns ecore::EInt:
	DIGIT+
;


terminal fragment DIGIT:
	"0".."9"
;

terminal fragment LETTER :
	"a".."z" | "A".."Z"
;



terminal WS:
	WHITESPACE_FRAGMENT+;

terminal EOL:
	LINE_TERMINATOR_SEQUENCE_FRAGMENT;




enum VersionComparator:
	Version = 'v'
|	Equals = '='
|	Smaller = '<'
|	Tilde = '~'
|	Caret = '^'
|	SmallerEquals = '<='
|	Greater = '>'
|	GreaterEquals = '>='
;
