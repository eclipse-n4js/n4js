/**
 * Copyright (c) 2016 NumberFour AG.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 * Contributors:
 *   NumberFour AG - Initial API and implementation
 */
grammar org.eclipse.n4js.n4jsx.N4JSX with org.eclipse.n4js.N4JS

import "http://www.eclipse.org/n4js/n4jsx/N4JSX"
import "http://www.eclipse.org/n4js/n4js/N4JS" as n4js
import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "http://www.eclipse.org/n4js/ts/Types" as types
import "http://www.eclipse.org/n4js/ts/TypeRefs" as types

IDLScript returns n4js::Script:
	super::Script;

/* see https://facebook.github.io/jsx/  */
@Override
PrimaryExpression <Yield> returns n4js::Expression:
	ThisLiteral
	| SuperLiteral
	| IdentifierRef<Yield>
	| JSXElement
	| ParameterizedCallExpression<Yield>
	| Literal
	| ArrayLiteral<Yield>
	| ObjectLiteral<Yield>
	| ParenExpression<Yield>
	| AnnotatedExpression<Yield>
	| FunctionExpression
	| AsyncFunctionExpression
	| N4ClassExpression<Yield>
	| TemplateLiteral<Yield>
;

JSXElement:
	'<' jsxElementName=JSXElementName JSXAttributes
	(('>' jsxChildren+=JSXChild* JSXClosingElement) | ('/' '>'));

fragment JSXClosingElement *:
	'<' '/' jsxClosingName=JSXElementName '>';

JSXChild:
	JSXElement | JSXExpression
	//	| JSXText -- not supported yet, cf. IDE-2414
;

// terminal JSX_TEXT: !('{'|'<'|'>'|'}'); needs to be refactored similar to template text, cf. IDE-2414

JSXExpression: '{' expression=AssignmentExpression<false,false> '}';

JSXElementName:
	expression=JSXElementNameExpression
	;

JSXElementNameExpression returns n4js::Expression:
	IdentifierRef<false>
	({n4js::ParameterizedPropertyAccessExpression.target=current} ParameterizedPropertyAccessExpressionTail<false>)*
	//	| JSXNamedspacedName not supported
;

//JSXNamedspacedName: JSXIdentifier ':' JSXIdentifier -- not supported in N4JSX
//JSXMemberExpression: JSXIdentifier '.' JSXIdentifier -- defined by means of ParameterizedPropertyAccessExpression

fragment JSXAttributes *:
	jsxAttributes+=JSXAttribute*;

JSXAttribute:
	JSXSpreadAttribute
	|
	JSXPropertyAttribute;

JSXSpreadAttribute:
	'{' '...' expression=AssignmentExpression<In=false, Yield=false> '}';

JSXPropertyAttribute:
	property=[types::IdentifiableElement|IdentifierName] ('=' (jsxAttributeValue=StringLiteral | ('{'
	jsxAttributeValue=AssignmentExpression<In=false, Yield=false> '}')))?;
