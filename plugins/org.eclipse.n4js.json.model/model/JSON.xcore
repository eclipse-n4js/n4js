/**
 * Copyright (c) 2016 NumberFour AG.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 * Contributors:
 *   NumberFour AG - Initial API and implementation
 */
@Ecore(nsURI="http://www.eclipse.org/n4js/JSON")
@GenModel(fileExtensions="json",
	modelDirectory="/org.eclipse.n4js.json.model/emf-gen",
	forceOverwrite="true",
	updateClasspath="false",
	literalsInterface="true",
	loadInitialization="false",
	complianceLevel="8.0",
	copyrightFields="false",
	// both copyrightText and documentation are required to align the output of the MWE2 and incremental builder (see GH-841)
	copyrightText="Copyright (c) 2016 NumberFour AG.\nAll rights reserved. This program and the accompanying materials\nare made available under the terms of the Eclipse Public License v1.0\nwhich accompanies this distribution, and is available at\nhttp://www.eclipse.org/legal/epl-v10.html\n\nContributors:\n  NumberFour AG - Initial API and implementation",
	documentation="*\nCopyright (c) 2016 NumberFour AG.\nAll rights reserved. This program and the accompanying materials\nare made available under the terms of the Eclipse Public License v1.0\nwhich accompanies this distribution, and is available at\nhttp://www.eclipse.org/legal/epl-v10.html\n * Contributors:\n  NumberFour AG - Initial API and implementation",
	language="")

package org.eclipse.n4js.json.JSON

/** Represents the root of a JSON document. */
class JSONDocument {
	/**
	 * The content of this JSON document.
	 */
	contains JSONValue content
}

/**
 * Common supertype to all values that may exist in JSON. 
 */
abstract class JSONValue {}

/**
 * Represents a JSON object.
 */
class JSONObject extends JSONValue {
	/**
	 * The name-value associations stored in this object.
	 */
	contains NameValuePair[] nameValuePairs
}

/**
 * Represents a JSON array.
 */
class JSONArray extends JSONValue {
	contains JSONValue[] elements
}

/**
 * Represents a single name-value association.
 */
class NameValuePair {
	String name
	contains JSONValue value
}

class JSONStringLiteral extends JSONValue {
	String value
}

class JSONNumericLiteral extends JSONValue {
	BigDecimal value
}

class JSONBooleanLiteral extends JSONValue {
	boolean ^booleanValue
}

class JSONNullLiteral extends JSONValue {}
