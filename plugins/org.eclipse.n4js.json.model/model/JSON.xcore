/**
 * Copyright (c) 2016 NumberFour AG.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 * Contributors:
 *   NumberFour AG - Initial API and implementation
 */
@Ecore(nsURI="http://www.eclipse.org/n4js/JSON")
@GenModel(fileExtensions="json",
	modelDirectory="/org.eclipse.n4js.json.model/emf-gen",
	forceOverwrite="true",
	updateClasspath="false",
	literalsInterface="true",
	loadInitialization="false",
	complianceLevel="8.0",
	copyrightFields="false",
	copyrightText="Copyright (c) 2016 NumberFour AG.\nAll rights reserved. This program and the accompanying materials\nare made available under the terms of the Eclipse Public License v1.0\nwhich accompanies this distribution, and is available at\nhttp://www.eclipse.org/legal/epl-v10.html\n\nContributors:\n  NumberFour AG - Initial API and implementation",
	language="")

package org.eclipse.n4js.json.JSON

class JSONDocument {
	contains JSONValue content
}

abstract class JSONValue {}

class JSONObject extends JSONValue {
	contains NameValuePair[] nameValuePairs
}

class JSONArray extends JSONValue {
	contains JSONValue[] elements
}

class NameValuePair {
	String name
	contains JSONValue value
}

class JSONStringLiteral extends JSONValue {
	String value
}

class JSONNumericLiteral extends JSONValue {
	BigDecimal value
}

class JSONBooleanLiteral extends JSONValue {
	boolean ^booleanValue
}

class JSONNullLiteral extends JSONValue {}