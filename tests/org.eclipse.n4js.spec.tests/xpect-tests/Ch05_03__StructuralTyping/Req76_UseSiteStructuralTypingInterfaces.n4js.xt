/*
 * Copyright (c) 2016 NumberFour AG.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 * Contributors:
 *   NumberFour AG - Initial API and implementation
 */

/* XPECT_SETUP org.eclipse.n4js.spec.tests.N4JSSpecTest
    ResourceSet {
		ThisFile {}
		File "Externals.n4jsd" {}
	}

   END_SETUP
 */

import * as Ex from "Ch05_03__StructuralTyping/Externals";

interface SomeInterface{
	public s : string;
	public foo(p : number) : void
	n : number;
	bar(p : string) : void
}
class Impl implements SomeInterface {
	@Override public s : string;
	@Override public foo(p : number) : void {}
	@Override n : number;
	@Override bar(p : string) : void {}
}

var someN4Object : SomeInterface;

// OK: nominal typing
function fNominal(p : SomeInterface) {
	p.foo(1);
	p.bar("Hello");
}

function fStructural(p : ~SomeInterface) {
	p.foo(1);
	p.s;
	// XPECT errors --> "method bar is not available for structurally referenced type ~SomeInterface." at "bar"
	p.bar("Hello");
}

function fStructuralFields(p : ~~SomeInterface) {
	// XPECT errors --> "method foo is not available for fields-only-referenced type ~~SomeInterface." at "foo"
	p.foo(1);
	p.s;
	// XPECT errors --> "method bar is not available for fields-only-referenced type ~~SomeInterface." at "bar"
	p.bar("Hello");
}


fNominal(new Impl());
fStructural(new Impl());
fStructuralFields(new Impl());

var a : Ex.A;
// XPECT errors --> "A is not a subtype of SomeInterface." at "a"
fNominal(a);
fStructural(a);
fStructuralFields(a);

var b : Ex.B;
// XPECT errors --> "B is not a structural subtype of ~SomeInterface: missing field s." at "b"
fStructural(b);
// XPECT errors --> "B is not a structural subtype of ~~SomeInterface: missing field s." at "b"
fStructuralFields(b);

var c : Ex.C;
// XPECT errors --> "C is not a structural subtype of ~SomeInterface: missing method foo." at "c"
fStructural(c);
fStructuralFields(c);


var d : Ex.D;
// XPECT errors --> "D is not a structural subtype of ~SomeInterface: foo failed: {function(string):void} is not a subtype of {function(number):void}." at "d"
fStructural(d);
fStructuralFields(d);
