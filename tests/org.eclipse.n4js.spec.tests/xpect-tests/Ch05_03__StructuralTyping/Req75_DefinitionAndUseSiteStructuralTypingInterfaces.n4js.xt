/*
 * Copyright (c) 2016 NumberFour AG.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 * Contributors:
 *   NumberFour AG - Initial API and implementation
 */

/* XPECT_SETUP org.eclipse.n4js.spec.tests.N4JSSpecTest
    ResourceSet {
		ThisFile {}
		File "Externals.n4jsd" {}
	}

 END_SETUP
 */

import * as Ex from "Ch05_03__StructuralTyping/Externals";

interface ~SomeInterface{
	public s : string;
	public foo(p : number) : void
	n : number;
	bar(p : string) : void
}
var someN4Object : SomeInterface;
class Impl implements SomeInterface {
	@Override public s : string;
	@Override public foo(p : number) : void {}
	@Override n : number;
	@Override bar(p : string) : void {}
}

// OK: nominal typing
function fDefSite(p : SomeInterface) {
	p.foo(1);
	// XPECT errors --> "method bar is not available for structurally defined type SomeInterface." at "bar"
	p.bar("Hello");
}

function fUseSite(p : ~SomeInterface) {
	p.foo(1);
	p.s;
	// XPECT errors --> "method bar is not available for structurally referenced type ~SomeInterface." at "bar"
	p.bar("Hello");
}

function fUseSiteFields(p : ~~SomeInterface) {
	// XPECT errors --> "method foo is not available for fields-only-referenced type ~~SomeInterface." at "foo"
	p.foo(1);
	p.s;
	// XPECT errors --> "method bar is not available for fields-only-referenced type ~~SomeInterface." at "bar"
	p.bar("Hello");
}


class SomeN4O {
	public s : string;
	public foo(p : number) : void {}
	private n : number;
	private bar(p : string) : void {}
}

fDefSite(new Impl());
fUseSite(new Impl());
fUseSiteFields(new Impl());

var someN4O : SomeN4O;
// XPECT errors --> "All N4Objects must explicitly extend/implement definition site structural type SomeInterface." at "someN4O"
fDefSite(someN4O);
// OK, use site
fUseSite(someN4O);
// OK, use site
fUseSiteFields(someN4O);

// same (nominal) type is always ok
fDefSite(new Impl());
fUseSite(new Impl());
fUseSiteFields(new Impl());

var a : Ex.A;
fDefSite(a);
fUseSite(a);
fUseSiteFields(a);

var b : Ex.B;
// XPECT errors --> "B is not a structural subtype of ~SomeInterface: missing field s." at "b"
fUseSite(b);
// XPECT errors --> "B is not a structural subtype of ~~SomeInterface: missing field s." at "b"
fUseSiteFields(b);

var c : Ex.C;
// XPECT errors --> "C is not a structural subtype of ~SomeInterface: missing method foo." at "c"
fUseSite(c);
// s is there, foo is ignored (fields only!)
fUseSiteFields(c);


var d : Ex.D;
// XPECT errors --> "D is not a structural subtype of ~SomeInterface: foo failed: {function(string):void} is not a subtype of {function(number):void}." at "d"
fUseSite(d);
// s is there, foo is ignored (fields only!)
fUseSiteFields(d);
