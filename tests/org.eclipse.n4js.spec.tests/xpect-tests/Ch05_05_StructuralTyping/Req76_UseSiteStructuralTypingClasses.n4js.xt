/*
 * Copyright (c) 2016 NumberFour AG.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 * Contributors:
 *   NumberFour AG - Initial API and implementation
 */

/* XPECT_SETUP org.eclipse.n4js.spec.tests.N4JSSpecTest
	ResourceSet {
		ThisFile {}
		File "Externals.n4jsd" {}
	}

 END_SETUP
 */

import * as Ex from "Ch05_05_StructuralTyping/Externals";

class SomeN4Object{
	public s : string;
	public foo(p : number) : void{};
	private n : number;
	private bar(p : string) : void{};
}
var someN4Object : SomeN4Object;


// OK: nominal typing
function fNominal(p : SomeN4Object) {
	p.foo(1);
	p.bar("Hello");
}

function fStructural(p : ~SomeN4Object) {
	p.foo(1);
	p.s;
	// XPECT errors --> "method bar is not available for structurally referenced type ~SomeN4Object." at "bar"
	p.bar("Hello");
}

function fStructuralFields(p : ~~SomeN4Object) {
	// XPECT errors --> "method foo is not available for fields-only-referenced type ~~SomeN4Object." at "foo"
	p.foo(1);
	p.s;
	// XPECT errors --> "method bar is not available for fields-only-referenced type ~~SomeN4Object." at "bar"
	p.bar("Hello");
}


fNominal(new SomeN4Object());
fStructural(new SomeN4Object());
fStructuralFields(new SomeN4Object());

var a : Ex.A;
// XPECT errors --> "A is not a subtype of SomeN4Object." at "a"
fNominal(a);
fStructural(a);
fStructuralFields(a);

var b : Ex.B;
// XPECT errors --> "B is not a structural subtype of ~SomeN4Object: missing field s." at "b"
fStructural(b);
// XPECT errors --> "B is not a structural subtype of ~~SomeN4Object: missing field s." at "b"
fStructuralFields(b);

var c : Ex.C;
// XPECT errors --> "C is not a structural subtype of ~SomeN4Object: missing method foo." at "c"
fStructural(c);
fStructuralFields(c);


var d : Ex.D;
// XPECT errors --> "D is not a structural subtype of ~SomeN4Object: foo failed: {function(string):void} is not a subtype of {function(number):void}." at "d"
fStructural(d);
fStructuralFields(d);


class G<T> {
	public s : T ;
}

function g(p : ~G<string> with {n : number;}) {
	// XPECT errors --> "int is not a subtype of string." at "1"
	p.s = 1;
	// XPECT errors --> "string is not a subtype of number." at ""hello""
	p.n = "hello";
}
