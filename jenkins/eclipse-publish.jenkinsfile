/*
 * Copyright (c) 2018 NumberFour AG.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 * Contributors:
 *   NumberFour AG - Initial API and implementation
 */


def MAVEN_OPTIONS = '-U -V -B -e '                                +
                    '-DWORKSPACE=$WORKSPACE '                     +
                    '-Dmaven.test.failure.ignore '                +
                    '-Dmaven.test.redirectTestOutputToFile=true ' +
                    '-Dtycho.localArtifacts=ignore'

def DEBUG_DEPLOY_SUFFIX = ''; // should be empty string by default; use something like '_GH-1234' for debugging the publishing build itself

// the following will be set below in stage 'Compute Version':
def String LANGUAGE_VERSION;
def String N4JS_LIBS_DIST_TAG;


pipeline {

    agent any

    options {
        buildDiscarder(
            logRotator(
                numToKeepStr:          '20',
                artifactDaysToKeepStr: '14',
                artifactNumToKeepStr:  '20',
                daysToKeepStr:         '14'))
        disableConcurrentBuilds()
        timeout(time: 3, unit: 'HOURS')
        timestamps()
    }

    // For how to request updates of below tools on Eclipse infrastructure, see file 'eclipse-nightly.jenkinsfile'.
    environment {
        NODEJS_PATH= '/shared/common/node-v12.13.1-linux-x64/bin'
        YARN_PATH  = '/shared/common/yarn/1.19.2/bin'
        PATH       = "${PATH}"                                          +
                     ':/shared/common/jq/1.6'                           +
                     ':/shared/common/apache-ant-1.9.6/bin'             +
                     ':/shared/common/maven/apache-maven-3.6.0/bin/'    +
                     ':/shared/common/node-v12.13.1-linux-x64/bin'      +
                     ':/shared/common/yarn/1.19.2/bin'                  +
                     ':/shared/common/java/openjdk/jdk-11/bin'
        MAVEN_OPTS = '-Xmx4G'
        JAVA_HOME  = '/shared/common/java/openjdk/jdk-11'
    }

    stages {

        stage('Compute Version') {
            steps {
                script {
                    sh 'n4js/releng/utils/scripts/compute-version.sh'
                    LANGUAGE_VERSION = sh(returnStdout: true, script: 'jq -r ".languageVersion" n4js/version-info.json').trim();
                    LANGUAGE_COMMIT = sh(returnStdout: true, script: 'jq -r ".languageCommit" n4js/version-info.json').trim();
                    N4JS_LIBS_DIST_TAG = sh(returnStdout: true, script: 'jq -r ".n4jsLibsDistTag" n4js/version-info.json').trim();
                    // note: we never publish any of the n4js-libs in this build,
                    // so we can ignore the other properties in file version-info.json
                }
            }
        }

        stage('Build (no tests)') {
            steps {
                dir('n4js') {
                    sh "mvn $MAVEN_OPTIONS -DlanguageVersion=${LANGUAGE_VERSION} -DnoTests clean verify"
                }

                // assert that the headless builder reports the correct version
                script {
                    def versionOfHeadlessBuilder = sh(returnStdout: true, script: 'java -jar n4js/plugins/org.eclipse.n4js.cli/target/n4jsc.jar --version').trim();
                    def expectedVersion = LANGUAGE_VERSION + " (commit " + LANGUAGE_COMMIT + ")"
                    if (versionOfHeadlessBuilder != expectedVersion) {
                        error "headless builder reported an incorrect version: ${versionOfHeadlessBuilder}"
                    }
                }
            }
        }

        stage('Deploy') {
            steps {
                script {
                    // append language version to file names of products
                    sh """
                        cd n4js/target
                        mv n4jsc.jar                n4jsc_${LANGUAGE_VERSION}.jar
                    """

                    echo "Deploying version ${LANGUAGE_VERSION} of N4JS ..."

                    echo 'START: moving old products to archive.eclipse.org (those that are older than a week):'
                    sh """\
                        cd /home/data/httpd/download.eclipse.org/n4js
                        find products/nightly -mindepth 1 -maxdepth 1 -type d -not -name LATEST -mtime +7 -print -exec mv {} /home/data/httpd/archive.eclipse.org/n4js/{} \\;
                    """
                    echo 'END: moving old products'

                    echo 'START: copying new products:'
                    def pathProductsVersion = "/home/data/httpd/download.eclipse.org/n4js/products$DEBUG_DEPLOY_SUFFIX/nightly/$LANGUAGE_VERSION";
                    sh """\
                        mkdir -p ${pathProductsVersion}
                        cp       n4js/target/*.jar           ${pathProductsVersion}
                    """
                    echo 'END: copying new products'

                    echo 'START: updating folder LATEST on download.eclipse.org:'
                    if (N4JS_LIBS_DIST_TAG == 'latest') {
                        def pathProductsLatest = "/home/data/httpd/download.eclipse.org/n4js/products$DEBUG_DEPLOY_SUFFIX/nightly/LATEST";
                        sh """\
                            rm -rf   ${pathProductsLatest}
                            mkdir -p ${pathProductsLatest}
                            cp -R    ${pathProductsVersion}/* ${pathProductsLatest}
                        """

                        // create copies of products without version suffix
                        sh """\
                            cp ${pathProductsLatest}/n4jsc_${LANGUAGE_VERSION}.jar               ${pathProductsLatest}/n4jsc.jar
                        """
                    } else {
                        echo 'SKIPPED! (because publishing with a dist-tag other than "latest")'
                    }
                    echo 'END: updating folder LATEST on download.eclipse.org:'

                    // show a summary
                    def output = sh returnStdout: true, script: """\
                        echo ******************** Products on download server:
                        ls -l /home/data/httpd/download.eclipse.org/n4js/products$DEBUG_DEPLOY_SUFFIX/nightly
                        echo ******************** Products on archive server:
                        ls -l /home/data/httpd/archive.eclipse.org/n4js/products/nightly
                        echo ********************
                    """

                    echo "$output"
                }
            }
        }
    }

    post {
        cleanup {
            deleteDir()
         }
    }
}
